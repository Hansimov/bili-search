// This file defines grammar for the DSL used by blbl.top search engine
// author: @Hansimov

start: expr | expr_with_error
    expr: and_expr+ | or_expr
        or_expr: and_expr (ORS and_expr)+
        and_expr: paren_expr | and_expr (ANDS and_expr)+ | atom_expr
            paren_expr: LP expr? RP
            atom_expr: date_expr | user_expr | uid_expr | stat_expr | region_expr | word_expr | text_plain
    expr_with_error: (expr | text_catch)+

// ---------- basic operators ---------- //
WT: " "
EQ: "="
MI: "-"
EQS: EQ+
NQ: "!" | "！"
LT: "<" | "《"
GT: ">" | "》"
AT: "@"

AND: "&"
OR: "|"
ANDS: AND+
ORS: OR+

NEQ: NQ EQ | MI EQ
NEQS: NQ EQS | MI EQS
LEQS: LT EQS
GEQS: GT EQS

op_rel: rel_eq | rel_lt | rel_gt | rel_neq | rel_leq | rel_geq
    rel_eq: EQS
    rel_lt: LT
    rel_gt: GT
    rel_neq: NEQS
    rel_leq: LEQS
    rel_geq: GEQS

op_list: list_eq | list_neq
    list_eq: EQS
    list_neq: NEQS

COMMA: "," | "，"
COLON: ":" | "："
LP: "(" | "（"
RP: ")" | "）"
LK: "[" | "【"
RK: "]" | "】"
lb: LP | LK
rb: RP | RK

lb_comma: lb COMMA* | COMMA*
comma_rb: COMMA* rb | COMMA*

digits: /[0-9]+/
text_strict: /[^\s\n:：!！,，（）【】《》\(\)\[\]]+/
text_plain:  /[^\s\n（）\|\&【】\(\)\[\]]+/
text_catch:  /[\s\n（）\|\&【】\(\)\[\]]+/

// ---------- date exprs ---------- //
date_expr: COLON? date_key date_op_val
    date_key: /(?i:(日期|时间|rq|date|dt|d))/
    date_op_val: date_op_val_single | date_op_val_list
        date_op_val_single: date_op_single date_val_single
            date_op_single: op_rel
            date_val_single: date_num_unit | date_iso | date_recent
                date_num_unit: date_num date_unit
                    date_num: digits
                    date_unit: date_unit_year | date_unit_month | date_unit_week | date_unit_day | date_unit_hour
                date_iso: yyyymmddhh | yyyymmdd | yyyymm | yyyy | mmddhh | mmdd
                    yyyymmddhh: yyyy sep_ymd mm sep_ymd dd sep_ymd hh
                    yyyymmdd: yyyy sep_ymd mm sep_ymd dd
                    yyyymm: yyyy sep_ymd mm
                    mmddhh: mm sep_ymd sep_dh hh
                    mmdd: mm sep_ymd dd
                date_recent: date_this | date_last | date_past
                    date_this: "this" sep_en date_unit
                    date_last: "last" sep_en date_unit
                    date_past: "past" sep_en date_num_unit
        date_op_val_list: date_op_list date_val_list
            date_op_list: op_list
            date_val_list: lb_comma (date_val_single)? (COMMA date_val_single)* comma_rb

yyyy: /\d{4}/
mm: /\d{1,2}/
dd: /\d{1,2}/
hh: /\d{1,2}/
sep_ymd: /[-\/\.]+/
sep_hms: /[_\.]+/
sep_dh: /[\s\.]+/
sep_en: /[\_\.\s\-]+/

date_unit_year: /(?i:(年|years?|yr|y))/
date_unit_month: /(?i:(个?月|months?|mon|m))/
date_unit_week: /(?i:(周|weeks?|wk|w))/
date_unit_day: /(?i:(日|天|days?|d))/
date_unit_hour: /(?i:(个?小时|时|hours?|hr|h))/


// ---------- user exprs ---------- //
user_expr: COLON? user_key user_op user_val | user_key_op user_val
    user_key: /(?i:(用户|昵称|作者|name|author|uploader|up|user|u))/
    user_op: EQ | NEQ
    user_key_op: user_eq | user_neq
        user_eq: AT
        user_neq: AT NQ
    user_val: user_val_single | user_val_list
        user_val_single: text_strict
        user_val_list: lb_comma (user_val_single)? (COMMA user_val_single)* comma_rb

uid_expr: COLON? uid_key uid_op_val
    uid_key: /(?i:(uid|mid))/
    uid_op_val: uid_op_val_single | uid_op_val_list
        uid_op_val_single: op_rel uid_val_single
            uid_val_single: digits
        uid_op_val_list: op_list uid_val_list
            uid_val_list: lb_comma (uid_val_single)? (COMMA uid_val_single)* comma_rb


// ---------- stat exprs ---------- //
stat_expr: COLON? stat_key stat_op_val
    stat_key: view_key | like_key | coin_key | favorite_key | reply_key | danmaku_key | share_key
        view_key: /(?i:(播放|bofang|bof|bf|view|vw|v))/
        like_key: /(?i:(点赞|dianzan|dianz|dz|like|lk|l))/
        coin_key: /(?i:(投币|toubi|toub|tb|coin|cn|c))/
        favorite_key: /(?i:(收藏|shouchang|shouc|sc|favorite|fav|fv|star|f))/
        reply_key: /(?i:(评论|回复|pinglun|pingl|huifu|huif|pl|hf|reply|rp|r))/
        danmaku_key: /(?i:(弹幕|danmu|danm|dm|danmaku|m))/
        share_key: /(?i:(分享|转发|fenxiang|fenx|zhuanfa|zhuanf|fx|zf|share|sh|s))/
    stat_op_val: stat_op_val_single | stat_op_val_list
        stat_op_val_single: op_rel stat_val_single
            stat_val_single: stat_num | stat_num stat_unit
                stat_num: digits
                stat_unit: /[百千kK万wWmM亿]+/
        stat_op_val_list: list_eq stat_val_list
            stat_val_list: lb_comma (stat_val_single)? (COMMA stat_val_single)* comma_rb


// ---------- region exprs ---------- //
region_expr: COLON? region_key region_op region_val
    region_key: /(?i:(分区|fenqu|fq|region|rid|r))/
    region_op: region_eq | region_neq
        region_eq: EQS
        region_neq: NEQS
    region_val: region_val_single | region_val_list
        region_val_single: digits | text_strict
        region_val_list: lb_comma (region_val_single)? (COMMA region_val_single)* comma_rb


// ---------- word exprs ---------- //
word_expr: COLON? word_key word_op word_val
    word_key: /(?i:(关键词|keyword|key|k))/
    word_op: word_eq | word_neq
        word_eq: EQS
        word_neq: NEQS
    word_val: word_val_single | word_val_list
        word_val_single: word_quoted | word
            word_quoted: /\"[^"]+\"/
            word: text_strict
        word_val_list: lb_comma (word_val_single)? (COMMA word_val_single)* comma_rb

%import common.WS
%ignore WS
