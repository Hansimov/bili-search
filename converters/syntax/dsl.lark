// This file defines grammar for the DSL used by blbl.top search engine
// author: @Hansimov

// Terms:
//  - expr: key + op + value
//  - key: key
//  - val: val
//  - op: operator
//  - lp/rp: left/right parenthesis ()
//  - lk/rk: left/right bracket     []
//  - lc/rc: left/right curly brace {}
//  - bles: blbl.top expression syntax

bles: expr+
    expr: date_expr | user_expr | uid_expr | stat_expr | region_expr

// ---------- basic operators ---------- //
ws: " "
eq: "="
mi: "-"
eqs: eq+
nq: "!" | "！"
lt: "<" | "《"
gt: ">" | "》"

neq: nq eq | mi eq
neqs: nq eqs
leqs: lt eqs
geqs: gt eqs

op_rel: eqs | lt | gt | neqs | leqs | geqs
op_list: eqs | neqs

comma: "," | "，"
colon: ":" | "："
lp: "(" | "（"
rp: ")" | "）"
lk: "[" | "【"
rk: "]" | "】"
lb: lp | lk
rb: rp | rk

lb_comma: lb comma* | comma*
comma_rb: comma* rb | comma*

digits: /[0-9]+/
text: /[^:：,，\[\]\(\)\n\s]+/


// ---------- date exprs ---------- //
date_expr: colon? date_key date_op_val
    date_key: /(?i:(日期|时间|rq|date|dt|d))/
    date_op_val: date_op_val_single | date_op_val_list
        date_op_val_single: date_op_single date_val_single
            date_op_single: op_rel
            date_val_single: date_num_unit | date_iso | date_recent
                date_num_unit: date_num date_unit
                    date_num: digits
                    date_unit: date_unit_year | date_unit_month | date_unit_week | date_unit_day | date_unit_hour
                date_iso: yyyymmddhh | yyyymmdd | yyyymm | yyyy | mmddhh | mmdd
                    yyyymmddhh: yyyy sep_ymd mm sep_ymd dd sep_ymd hh
                    yyyymmdd: yyyy sep_ymd mm sep_ymd dd
                    yyyymm: yyyy sep_ymd mm
                    mmddhh: mm sep_ymd sep_dh hh
                    mmdd: mm sep_ymd dd
                date_recent: date_this | date_last | date_past
                    date_this: "this" sep_en date_unit
                    date_last: "last" sep_en date_unit
                    date_past: "past" sep_en date_num_unit
        date_op_val_list: date_op_list date_val_list
            date_op_list: op_list
            date_val_list: lb_comma (date_val_single)? (comma date_val_single)* comma_rb

yyyy: /\d{4}/
mm: /\d{1,2}/
dd: /\d{1,2}/
hh: /\d{1,2}/
sep_ymd: /[-\/\.]+/
sep_hms: /[_\.]+/
sep_dh: /[\s\.]+/
sep_en: /[\_\.\s\-]+/

date_unit_year: /(?i:(年|years?|yr|y))/
date_unit_month: /(?i:(个?月|months?|mon|m))/
date_unit_week: /(?i:(周|weeks?|wk|w))/
date_unit_day: /(?i:(日|天|days?|d))/
date_unit_hour: /(?i:(个?小时|时|hours?|hr|h))/


// ---------- user exprs ---------- //
user_expr: colon? user_key user_op user_val | user_key_op user_val
    user_key: /(?i:(用户|昵称|作者|name|author|uploader|up|user|u))/
    user_op: eq | neq
    user_key_op: "@" | "@!"
    user_val: user_val_single | user_val_list
        user_val_single: /[^:：,，\[\]\(\)\n\s]+/
        user_val_list: lb_comma (user_val_single)? (comma user_val_single)* comma_rb

uid_expr: colon? uid_key uid_op_val
    uid_key: /(?i:(uid|mid))/
    uid_op_val: uid_op_val_single | uid_op_val_list
        uid_op_val_single: op_rel uid_val_single
            uid_val_single: digits
        uid_op_val_list: op_list uid_val_list
            uid_val_list: lb_comma (uid_val_single)? (comma uid_val_single)* comma_rb

// ---------- stat exprs ---------- //
stat_expr: colon? stat_key stat_op_val
    stat_key: view_key | like_key | coin_key | favorite_key | reply_key | danmaku_key | share_key
        view_key: /(?i:(播放|bofang|bof|bf|view|vw|v))/
        like_key: /(?i:(点赞|dianzan|dianz|dz|like|lk|l))/
        coin_key: /(?i:(投币|toubi|toub|tb|coin|cn|c))/
        favorite_key: /(?i:(收藏|shouchang|shouc|sc|favorite|fav|fv|star|f))/
        reply_key: /(?i:(评论|回复|pinglun|pingl|huifu|huif|pl|hf|reply|rp|r))/
        danmaku_key: /(?i:(弹幕|danmu|danm|dm|danmaku|m))/
        share_key: /(?i:(分享|转发|fenxiang|fenx|zhuanfa|zhuanf|fx|zf|share|sh|s))/
    stat_op_val: stat_op_val_single | stat_op_val_list
        stat_op_val_single: op_rel stat_val_single
            stat_val_single: stat_num | stat_num stat_unit
                stat_num: digits
                stat_unit: /[百千kK万wWmM亿]+/
        stat_op_val_list: op_list stat_val_list
            stat_val_list: lb_comma (stat_val_single)? (comma stat_val_single)* comma_rb


// ---------- region exprs ---------- //
region_expr: colon? region_key region_op region_val
    region_key: /(?i:(分区|fenqu|fq|region|rid|r))/
    region_op: eq | neq
    region_val: region_val_single | region_val_list
        region_val_single: digits | /[^:：,，\[\]\(\)\n\s]+/
        region_val_list: lb_comma (region_val_single)? (comma region_val_single)* comma_rb


%import common.WS
%ignore WS
